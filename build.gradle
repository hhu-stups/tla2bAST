plugins {
	id "application"
	id 'java-library'
	id 'eclipse'
	id 'maven-publish'
	id 'jacoco'
	id 'signing'

	id "com.gradleup.shadow" version "8.3.9"
	id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
}

project.group = 'de.hhu.stups'
project.version = "1.4.3-SNAPSHOT"
final isSnapshot = project.version.endsWith("-SNAPSHOT")

final snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
final releasesStagingRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/"
repositories {
	mavenCentral()
	if (isSnapshot) {
		maven {
			name = "Sonatype snapshots"
			url = snapshotsRepoUrl
		}
	}
}

configurations.configureEach {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def parserVersion = "2.15.4"

dependencies {
	implementation("commons-cli:commons-cli:1.11.0")
	api("de.hhu.stups:tlatools:1.1.0")
	api("de.hhu.stups:prologlib:${parserVersion}")
	api("de.hhu.stups:bparser:${parserVersion}")

	testImplementation(platform("org.junit:junit-bom:5.14.2"))
	testImplementation("org.junit.jupiter:junit-jupiter")
	testCompileOnly("junit:junit:4.13.2")
	testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8

	withSourcesJar()
	withJavadocJar()
}

javadoc {
	// silence warnings on missing javadoc
	options.addBooleanOption('Xdoclint:all,-missing', true)
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

test {
	useJUnitPlatform()

	testLogging {
		events 'passed', 'skipped', 'failed'
	}
}

application {
	mainClass = "de.tla2b.TLA2B"
}

jar {
	manifest {
		attributes([
			"Main-Class": application.mainClass,
		])
	}
}

shadowJar {
	archiveFileName = 'TLA2B.jar'
}

// Don't publish the shadowJar to Maven Central. Code from:
// https://github.com/johnrengelman/shadow/issues/586#issuecomment-708375599
// https://github.com/johnrengelman/shadow/issues/651#issuecomment-815921124
components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
	skip()
}

processResources {
	inputs.property("version", project.version)
	filesMatching("de/tla2b/build.properties") {
		expand(version: inputs.properties["version"])
	}
}

jacoco {
	toolVersion = "0.8.14"
	reportsDirectory = file("$buildDir/customJacocoReportDir")
}

// type 'gradle tla2b jacocoIntegrationTestReport' in order to run the jacoco code coverage analysis
tasks.register('jacocoIntegrationTestReport', JacocoReport) {
	sourceSets sourceSets.main
	//executionData files('build/jacoco/integrationTests.exec')
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = 'TLA+ to B-AST'
				description = "Translator from TLA+ to ProB's AST representation."
				url = 'https://github.com/hhu-stups/tla2bAST'

				licenses {
					license {
						name = 'Eclipse Public License, Version 1.0'
						url = 'https://www.eclipse.org/legal/epl-v10.html'
					}
				}

				scm {
					connection = 'scm:git:https://github.com/hhu-stups/tla2bAST.git'
					developerConnection = 'scm:git:git@github.com:hhu-stups/tla2bAST.git'
					url = 'https://github.com/hhu-stups/tla2bAST'
				}

				developers {
					developer {
						id = 'bendisposto'
						name = 'Jens Bendisposto'
						email = 'jens@bendisposto.de'
					}
				}
			}
		}
	}
}

nexusPublishing {
	repositories {
		mavenCentral {
			snapshotRepositoryUrl.set(uri(snapshotsRepoUrl))
			nexusUrl.set(uri(releasesStagingRepoUrl))
		}
	}
}

ext."signing.secretKeyRingFile" = rootProject.file("secring.gpg").absolutePath

signing {
	sign publishing.publications.mavenJava
}
